#map goto 1;
#var MAPPER_SYNC 0;
#map flag vtmap on;
#map flag unicodegraphics on;
#map flag static on;

#alias {move_n} {#send n;#var trytomove 1;#var movedir {polnoc}};
#alias {move_s} {#send s;#var trytomove 1;#var movedir {poludnie}};
#alias {move_e} {#send e;#var trytomove 1;#var movedir {wschod}};
#alias {move_w} {#send w;#var trytomove 1;#var movedir {zachod}};
#alias {move_d} {#send d;#var trytomove 1;#var movedir {dol}};
#alias {move_u} {#send u;#var trytomove 1;#var movedir {gora}};
#alias {move_ne} {#send ne;#var trytomove 1;#var movedir {polnocny-wschod}};
#alias {move_nw} {#send nw;#var trytomove 1;#var movedir {polnocny-zachod}};
#alias {move_sw} {#send sw;#var trytomove 1;#var movedir {poludniowy-zachod}};
#alias {move_se} {#send se;#var trytomove 1;#var movedir {poludniowy-wschod}};

#alias {n} {move_n};
#alias {s} {move_s};
#alias {ne} {move_ne};
#alias {nw} {move_nw};
#alias {sw} {move_sw};
#alias {se} {move_se};
#alias {d} {move_d};
#alias {u} {move_u};
#alias {w} {move_w};
#alias {e} {move_e};


#nop #################################################################################;
#nop EVENTY GMCP;
#nop #################################################################################;
#event {IAC SB GMCP room.info IAC SE} {
    #var roza_custom {};
    #var ROOM_INFO {%0};
    #var room_desc ${ROOM_INFO[map][x]}x${ROOM_INFO[map][y]};
    #var room_info_hybrid $ROOM_INFO;
    #var b {0};

    #var rozk2 {{w}{ }{e}{ }{n}{ }{s}{ }{nw}{ }{ne}{ }{se}{  }{sw}{  }{u}{ }{d}{ }{wyjscie}{   }{nw2}{ }{n2}{   }{ne2}{   }{w2}{ }{e2}{ }{sw2}{   }{s2}{   }{se3}{   }};

    #foreach {$ROOM_INFO[exits]} {i}
    {
      #switch {"$ROOM_INFO[exits][$i]"}
      {
        #case {"polnoc"} {#math {b} {$b + 1};#var {rozk[n]} { |};#var {rozk2[n]} {|};#var {rozk[n2]}{ N };};
        #case {"polnocny-wschod"} {#math {b} {$b + 1};#var {rozk2[ne]} {/};#var {rozk[ne]} {/  };#var {rozk[ne2]}{ NE};};
        #case {"polnocny-zachod"} {#math {b} {$b + 1};#var {rozk2[nw]} {\\};#var {rozk[nw]} {  \\};#var {rozk[nw2]}{NW };};
        #case {"poludnie"} {#math {b} {$b + 1};#var {rozk2[s]} {|};#var {rozk[s]} { |};#var {rozk[s2]}{ S };};
        #case {"poludniowy-wschod"} {#math {b} {$b + 1};#var {rozk2[se]} {\\ };#var {rozk[se]} {\\  };#var {rozk[se2]}{ SE};};
        #case {"poludniowy-zachod"} {#math {b} {$b + 1};#var {rozk2[sw]} { /};#var {rozk[sw]} {  /};#var {rozk[sw2]}{SW };};
        #case {"wschod"} {#math {b} {$b + 1};#var {rozk2[e]} {-};#var {rozk[e]} {---};#var {rozk[e2]}{E};};
        #case {"zachod"} {#math {b} {$b + 1};#var {rozk2[w]} {-};#var {rozk[w]} {---};#var {rozk[w2]}{W};};
        #case {"dol"} {#math {b} {$b + 1};#var {rozk2[d]} {V}};
        #case {"gora"} {#math {b} {$b + 1};#var {rozk2[u]} {A}};
        #default {
            #format defexitlen {%L} {$ROOM_INFO[exits][$i]};
            #if {$defexitlen > 2} {
                #var roza_custom {$ROOM_INFO[exits][$i]};
                #nop line substitute {variables} {#macro {\eOn} {$ROOM_INFO[exits][$i]};};
            };
        };
      };
    };

    
    #if {$profiles_roza == 3} {
        #map map 30x15;
    };
    #if {$frozen_status == 1} {
        frozen_unset;
    };

    #if {$semifrozen_status == 1} {
        #math {semifrozen_steps} {$semifrozen_steps - 1};
        #if {$semifrozen_steps < 1} {
            semifrozen_unset;
        };
    };
    #if {$ROOM_INFO[map][x] > 0} {
        #var current_room_short_desc {$ROOM_INFO[map][name] $ROOM_INFO[map][x] $ROOM_INFO[map][y]};
    };

    #if {$trytomove > 0} {
        #var trytomove 0;
        #map get roomvnum roomId;
    };
};

#event {MAP ENTER ROOM} {
    #var r_curLineCnt 2;
    #var prmptbottomv 1;
    #map get roomvnum voom;
    #map get roomsymbol {voos_num};
    #var note_found 0;
    #var bind_found 0;
    #if {"$voos_num" == "<178>i<088>"} {
        #var note_found 1;
    };
};

#alias {UNFOLD} {
    #if {"$unfolded" != "1"} {
        #format screen_r {%R};
        #math screen_r_half {($screen_r / 3) - 2};
        #math screen_r_half_title {($screen_r / 3) - 2 - 1};
        #split $screen_r_half $prmptbottomv;
        hpbar;
        #var unfolded 1;
    };
};

#alias {UNFOLD2} {
    #if {"$unfolded" != "2"} {
        #format screen_r {%R};
        #math screen_r_half {$screen_r / 2};
        #math screen_r_half_title {($screen_r / 2) - 1};
        #split $screen_r_half $prmptbottomv;
        hpbar;
        #var unfolded 2;
    };
};


#alias {FOLD} {
    #if {$unfolded > 0} {
        #var screen_r_half 0;
        #split $screen_r_half $prmptbottomv;
        hpbar;
        #var unfolded 0;
    };
};

#alias {FOLDER} {
	#switch {"$unfolded"} {
		#case {"1"} {UNFOLD2};
		#case {"2"} {FOLD};
		#default {UNFOLD};
	};
}


#nop var zadania[1] {quest};

#alias {/zadania} {
    #var cpcmd {1};

    #foreach {$zadania[]} {zadd} {
        #unvar checkpoints;
        #map list {} {} {} {$zadania[$zadd]} {variable} {checkpoints};
        #list vcp create {$checkpoints};
        #list vcp {size} {vcpSize};

        #foreach {$checkpoints[]} {vcpv} {
            #if {$checkpoints[$vcpv] > 1} {
                #if {$checkpoints[$vcpv] < 100} {

                    #map at {$vcpv} {
                        #map get roomnote cp_roomnote;
                        #map get roomdesc cp_roomdesc;
                        #map get roomarea cp_roomarea;
                    };

                    #var {cp_command} {run${cpcmd}};
                    #var {cp_command2} {safe${cpcmd}};
                    #line substitute {variables} {#alias {$cp_command} {
                        semifrozen_set;#map run {$vcpv} {0.5};
                        #math semifrozen_steps {$checkpoints[$vcpv] - 1};
                    }};
                    #line substitute {variables} {#alias {$cp_command2} {
                        semifrozen_set;#map run {$vcpv} {0.2};
                        #math semifrozen_steps {$checkpoints[$vcpv] - 1};
                    }};

                    #format {cpd_runcmd} {%-2s %.2s} {$cpcmd};
                    #format {cpd_area} {%-7s %.7s} {$cp_roomarea};
                    #format {cpd_distance} {%+6s %.6s} {$checkpoints[$vcpv]};
                    #format {cpd_note} {%-20s %.20s} {$cp_roomnote};
                    #format {cpd_short} {%-20s %.20s} {$cp_roomdesc};
                    #format {cpd_roomid} {%+6s %.6s} {$vcpv};
                    #format {cp_command} {%+6s} {run${cpcmd}};
                    #nop echo {$checkpoints[$vcpv] $vcpv $cp_roomarea $cp_roomdesc};
                    #echo {$cpd_runcmd Dyst: $cpd_distance $c_rooms[$zadania[$zadd]]$cpd_area:<088> $cp_roomnote};
                    #math {cpcmd} {$cpcmd +1};
                };
            };
        };
    };

} {1};



#nop #################################################################################;
#nop ODNAJDYWANIE SIE NA MAPIE
#nop #################################################################################;
    #nop ##################################;
    #nop funkcja szukajaca;
    #nop map list <name> <exits> <desc> <area> <note> <terrain>;
    #nop ##################################;
#var current_room_short_desc {unknown2};
#var current_room_exits {unknown2};
#alias {/f$} {findme} {1}?; 
#nop alias findme {#echo a};
#alias findme {
        #map get roomvnum rmvnum;
        #var widocznewyjscia {$ROOM_INFO[exits]};
        #var exitList {};
        #var vexits {};

        #var roomfound 0;

    #nop ##################################;
    #nop szukaj pokoju wedlug jego opisu
    #nop w lokacjach gmcp, opis jest koordynata
    #nop w reszcie lokacji, ostatnio widziany short w danej lokacji
    #nop
    #nop jesil jest jeden rezultat, przenies mape
    #nop ##################################;

        #nop var k @_print_warn{Szukam pokoju <118><aff>$current_room_short_desc<088>};
        #map list {} {} {$current_room_short_desc} {variable} {vexits};
 	#list vexitslist create {$vexits};
 	#list vexitslist {size} {listsize};
        #if {$listsize == 2} {
            #var roomfound {1};
            #if {$vexitslist[1] != $rmvnum} {
                #ec {Odnalazlem mapke na: $vexitslist[1]<088>};
                #map goto $vexitslist[1];
            #nop map map ${screen_w}x15;
                #var roomId $vexitslist[1];
                #var MAPPER_SYNC $vexitslist[1];
                #map get roomdata tmproomdata;
            };
        };

    #nop ##################################;
    #nop szukamy pokoju wedlug opisu i dostepnych wyjsc
    #nop ##################################;

    #if {$roomfound == 0} {
        #var vexits {};
        #list vexitslist clear;
        #map list {$current_room_exits} {} {$current_room_short_desc} {variable} {vexits};
 	#list vexitslist create {$vexits};
        #list vexitslist {size} {listsize};
        #list vexitslist_filter clear;
        #var velf 0;

        #if {$listsize == 2} {
            #var roomfound {1};
            #if {$vexitslist[1] != $rmvnum} {#ec {Odnalazlem mapke na: $vexitslist[1]<088>};#map goto $vexitslist[1];#var roomId $vexitslist[1];#var MAPPER_SYNC $vexitslist[1]};
            #var MAPPER_SYNC $vexitslist[1];        
        };
        #if {$listsize > 2} {
            #if {$listsize < 4} {
                #echo {Znalazlem pokoj <118><ffa>$current_room_short_desc<088> z wyjsciami na: <118><aff>$current_room_exits<088> $listsize razy.<088>};
                
                #var nearestexitdistance 100000;
                #var nearestroomid $rmvnum;
                #foreach {$vexits[]} {exitvector} {
                    #sh $exitvector $vexits[$exitvector];
                    #if {$vexits[$exitvector] < $nearestexitdistance} {
                        #var nearestexitdistance $vexits[$exitvector];
                        #var nearestroomid $exitvector;
                    };
                };
                #if {$nearestroomid != $rmvnum} {
                    #ec {Odnalazlem mapke na: $nearestroomid<088>};
                    #map goto $nearestroomid;
                    #var roomId $nearestroomid;
                    #var MAPPER_SYNC $nearestroomid;
                    #map get roomdata tmproomdata;
                };
            };
        };
    };
};
#alias {mfind} {
    #loop {1} {17500} {i} {
        #map at {$i} {#map get roomdesc mfindroomdesc};
        #regex {$mfindroomdesc} {%0} {#sh $i};
    }
}



#nop #################################################################################;
#nop KOMENDY UNDO I FREEZE;
#nop #################################################################################;
#alias undo {
    #if {$trytomove == 1} {
        #map undo;
        #map goto $roomId;
        #var trytomove 0;
    };
    #if {$semifrozen_status == 1} {
        semifrozen_unset;   #nop wracam do zwyklego trybu;
        #undelay {PATH %*}; #nop zatrzymuje reszte komendy;
    };
}
#alias {semifrozen_set} {
    #var c_panelbg {<BBA>};
    #var semifrozen_status {1};
    hpbar;    
}
#alias {semifrozen_unset} {
    #var c_panelbg {<AAB>};
    #var semifrozen_status {0};
    hpbar;
}
#alias {frozen_set} {
    #var c_panelbg {<BAA>};
    #var frozen_status {1};
    #alias {move_n} {#echo {${c_stan_niski}Nie mozesz teraz sie ruszyc.}};
    #alias {move_s} {#echo {${c_stan_niski}Nie mozesz teraz sie ruszyc.}};
    #alias {move_e} {#echo {${c_stan_niski}Nie mozesz teraz sie ruszyc.}};
    #alias {move_w} {#echo {${c_stan_niski}Nie mozesz teraz sie ruszyc.}};
    #alias {move_d} {#echo {${c_stan_niski}Nie mozesz teraz sie ruszyc.}};
    #alias {move_u} {#echo {${c_stan_niski}Nie mozesz teraz sie ruszyc.}};
    #alias {move_ne} {#echo {${c_stan_niski}Nie mozesz teraz sie ruszyc.}};
    #alias {move_nw} {#echo {${c_stan_niski}Nie mozesz teraz sie ruszyc.}};
    #alias {move_sw} {#echo {${c_stan_niski}Nie mozesz teraz sie ruszyc.}};
    #alias {move_se} {#echo {${c_stan_niski}Nie mozesz teraz sie ruszyc.}};
    hpbar;    
}
#alias {frozen_unset} {
    #var c_panelbg {<AAB>};
    #var frozen_status {0};
    #alias {move_n} {#send n;#var trytomove 1;#var movedir {polnoc}};
    #alias {move_s} {#send s;#var trytomove 1;#var movedir {poludnie}};
    #alias {move_e} {#send e;#var trytomove 1;#var movedir {wschod}};
    #alias {move_w} {#send w;#var trytomove 1;#var movedir {zachod}};
    #alias {move_d} {#send d;#var trytomove 1;#var movedir {dol}};
    #alias {move_u} {#send u;#var trytomove 1;#var movedir {gora}};
    #alias {move_ne} {#send ne;#var trytomove 1;#var movedir {polnocny-wschod}};
    #alias {move_nw} {#send nw;#var trytomove 1;#var movedir {polnocny-zachod}};
    #alias {move_sw} {#send sw;#var trytomove 1;#var movedir {poludniowy-zachod}};
    #alias {move_se} {#send se;#var trytomove 1;#var movedir {poludniowy-wschod}};
    hpbar;
}
#nop WYJATKI I SMIECI;
#nop #################################################################################;
#action {Ciemne miejsce.} {
    #var current_room_short_desc {unknown2};
    qr zapal lampe;
};
