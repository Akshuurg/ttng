#event {PROGRAM START} {
	#var cport %0;
    #port uninit ${cport}5;
    #port init mapka ${cport}5;
    
	#map read bundles/TTMap/map.map;
	#map flag vtmap on;
	#map flag unicodegraphics on;
    #map goto 1;
	#config mouse on;
	#delay {3} {
		#format sw {%R};
		#split $sw 0;
		#format {screen_rows} {%R};
	    #format {screen_cols} {%C};
	    #math deltaY {$screen_cols / 10};
	    #math deltaX {$splitrows / 10};
	};
};
#event {SCREEN RESIZE} {
    #format sw {%R};
    #split $sw 0;
}

#var c_arkatt {<168>};
#var c_rooms[vendor] {<158>};
#var c_rooms[checkpoint] {<168>};
#var c_rooms[water] {<148>};
#var c_rooms[bank] {<138>};
#var c_rooms[post] {<128>};
#var c_rooms[safe] {<afa>};
#var c_rooms[food] {<128>}
#var c_rooms[quest] {<138>};
#var c_rooms[info] {<178>};
#var c_rooms[danger] {<118>};
#var c_rooms[port] {<118><ccf>};
#var c_rooms[herb] {<118><fac>};
#var c_rooms[trap] {<FAA><178>};
#var c_rooms[smith] {<118><caf>};
#var c_rooms[knowledge] {<078>};
#var c_rooms[training] {<038>};
#var c_rooms[storage] {<118><fca>};

#action {chats to you, '%d'} {
	#map goto %1;
	#line gag;
};

#event {MAP DOUBLE-CLICKED MOUSE BUTTON ONE} {
    #chat message all {WALK %0};
};


#var MapLegendFolded {1};

#event {PRESSED MOUSE BUTTON ONE}
{
    #var PRESSED 1;
    #var centreY {%0};
    #var centreX {%1};

#sh {$centreY $centreX $MapLegendFolded};
    #if {$centreY == 1} {
	#if {$centreX < 7} {
		#if {$MapLegendFolded == 1} {
			#var MapLegendFolded 0;
        		/zadania;
	#delay {0.3} {
    			#sh {<ABA><178> MAPA <088>} {1} {1};
			#foreach {$cplist} {cpl} {
				#if {$cpl <= $sw} {
					#if {$cpl > 1} {
					#sh {$cplist[$cpl]} {$cpl} {1};
					};
        			};
    			};    
			#nop UNFOLD;
	};
		} {
			#var MapLegendFolded 1;
			#map goto $currentRoom;
			#nop FOLD;
		};
	};
    } {
	#if {$MapLegendFolded == 0} {
	    	#if {$centreX < 4} {
        		walk$centreY;
    		};
    	};
    };
}

#event {RELEASED MOUSE BUTTON ONE}
{
    #var PRESSED 0;
    #if {"$moving" == "true"} {
        #undelay {PATH %*}
    };
}

#var eventcounter 0;
#event {MOVED MOUSE BUTTON ONE}
{
    #math {eventcounter} {$eventcounter + 1};

#if {$eventcounter > 2} {
    #var eventcounter 0;

    #if {$PRESSED > 0}
    {
        #var currentX {%1};
        #var currentY {%0};

#nop DIFF Y;
        #if {$currentY > $centreY} {
            #math diffY {$currentY - $centreY};
        } {
            #math diffY {$centreY - $currentY};
        };
#nop DIFF X;
        #if {$currentX > $centreX} {
            #math diffX {$currentX - $centreX};
        } {
            #math diffX {$centreX - $currentX};
        };

        #echo {<128>CTR:(Y:$centreY X:$centreX) <118>NOW:(Y:$currentX X:$currentX)<088>};

        #if {$currentY > $centreY} {
            #var dirH {n};
        };
        #elseif {$currentY < $centreY} {
            #var dirH {s};
        };
        #else {
            #var dirH {};
        };

        #if {$currentX > $centreX} {
            #var dirV {w}
        };
        #elseif {$currentX < $centreX} {
            #var dirV {e};
        };
        #else {
            #var dirV {};
        };
        #var dir {${dirH}${dirV}};
        #map get roomexits currentroomexits;

        #if {$currentroomexits[$dir] > 0} {
            #map move $dir;
        };
        #echo {<178>$dir};
    };
};
}


#action {<CHAT> %S chats to you, 'MCREATE %d %w'} {
    #map at {%2} {#map dig %3};
    loading_banner;
    #line gag;
} {1};

#action {<CHAT> %S chats to you, 'MARK %1 %2'} {
    #map at {%1} {#map set roomarea %2};
    #map at {%1} {#map set roomcolor $c_rooms[%2]};
    loading_banner;
    #line gag;
} {1};


#action {<CHAT> %S chats to you, 'DEL %1 %2'} {
    #map at {%1} {#map del %2};
    loading_banner;
    #line gag;
} {1};


#action {<CHAT> %S chats to you, 'NOTE %1'} {
    #map at {%1} {#map set roomsymbol {<178>!<088>}}
    loading_banner;
    #line gag;
} {1};


#action {<CHAT> %S chats to you, 'HIDE %1 %2'} {
    #map at {%1} {
        #line substitute variables {
            #map exitflag %2 hide on;
        };
    };
    loading_banner;
    #line gag;
} {1};

#action {<CHAT> %S chats to you, 'UNHIDE %1 %2'} {
    #map at {%1} {
        #line substitute variables {
            #map exitflag %2 hide off;
        };
    };
    loading_banner;
    #line gag;
} {1};

#action {<CHAT> %S chats to you, 'LINKB %d %w %d'} {
    #map at {%2} {
        #line substitute variables {
            #map link %3 %4 both;
        };
    };
    #line gag;
} {1};

#action {<CHAT> %S chats to you, 'UNLINKB %d %w'} {
    #map at {%2} {
        #line substitute variables {
            #map unlink %3 both;            
        };
    };
    loading_banner;
    #line gag;
} {1};

#action {<CHAT> %S chats to you, 'LINK %d %w %d'} {
    #map at {%2} {
        #line substitute variables {
            #map link %3 %4;
        };
    };
    loading_banner;
    #line gag;
} {1};

#action {<CHAT> %S chats to you, 'UNLINK %d %w'} {
    #map at {%2} {
        #line substitute variables {
            #map unlink %3;         
        };
    };
    loading_banner;
    #line gag;
} {1};


#action {<CHAT> %S chats to you, 'MEXIT %d %w %*'} {
    #map goto %2;
    #map exit %3 command {%4};
    #map move %3;
    #map get roomvnum tmproomvnum;
    #map undo;
    #map link {%4} $tmproomvnum;
    loading_banner;
    #line gag;
};


#alias {loading_banner} {
    #sh {<FFA><118><aaa>WCZYTUJE...<088>} {-3};
    #undelay {quickbinddelay};
    #delay {quickbinddelay} {#sh { } {-3};} {2};
};

#alias {downloading_banner} {
    #sh {<FFA><128><aaa>SCIAGAM %1<088>} {-3};
    #undelay {downloader};
    #delay {downloader} {#sh { } {-3};} {1};
};

#alias {loading_banner} {
    #sh {<FFA><118><aaa>WCZYTUJE...<088>} {-3};
    #undelay {quickbinddelay};
    #delay {quickbinddelay} {#sh { } {-3};} {2};
};

#alias {downloading_banner} {
    #sh {<FFA><128><aaa>SCIAGAM %1<088>} {-3};
    #undelay {downloader};
    #delay {downloader} {#sh { } {-3};} {1};
};


#action {chats to you, 'EVENT %d'} {
    #map at {%1} {#map set roomsymbol {<168>@<088>}};
    loading_banner;
    #line gag;
} {1};

#action {chats to you, 'ROOMSHORT %d'} {
    #map at {%1} {
        #map get roomsymbol vroomsymbol;
        #if {"$vroomsymbol" == "<faa>?<088>"} {
            #map set roomsymbol {};
        };
    };
    loading_banner;
    #line gag;
};

#action {chats to you, 'ROOMEXITS %d'} {
    #map at {%1} {
        #map get roomcolor vroomcolor;
        #if {"$vroomcolor" == "<faa>"} {
            #map set roomcolor {};
        };
    };
    loading_banner;
    #line gag;
};

#action {chats to you, 'COLOR %d %S'} {
    #var tmpsymbol {%2};
    #replace {tmpsymbol} {-} {<};
    #replace {tmpsymbol} {+} {>};
    #map at {%1} {#map set roomcolor {$tmpsymbol}};
    loading_banner;
    #line gag;
};

#action {chats to you, 'DOWNLOAD MAP'} {
    downloading_banner MAPKE;
};

#event {MAP UPDATED VTMAP} {
    #sh {<ABA><178> MAPA <088>} {1} {1};
};
#event {MAP ENTER ROOM} {
    #var MapLegendFolded 1;
    #map get roomvnum CurrentRoom;
};

#var zadania[1] {quest};
#var zadania[2] {checkpoint};
#var zadania[3] {vendor};
#var zadania[4] {smith};

#alias {/zadania} {
    #unvar cplist;
    #var cplist[1] {};
    #var cpcmd {2};

    #foreach {$zadania[]} {zadd} {
        #unvar checkpoints;
        #map list {} {} {} {$zadania[$zadd]} {variable} {checkpoints};

        #foreach {$checkpoints[]} {vcpv} {
            #math vcpvcheck {0};
            #math vcpvcheck {$vcpvcheck + $checkpoints[$vcpv][distance]};
            #if {$vcpvcheck > 1} {
                #if {$vcpvcheck < 100} {
                    #map at {$vcpv} {
                        #map get roomnote cp_roomnote;
                        #map get roomdesc cp_roomdesc;
                        #map get roomarea cp_roomarea;
                    };
                    #line substitute variables {
                        #alias {walk$cpcmd} {
                            #port send $connectorId {WALK $vcpv};
                        };
                    };
                    
                    #format {cpd_runcmd} {%-2s %.2s} {$cpcmd};
                    #format {cpd_area} {%-7s %.7s} {$cp_roomarea};
                    #format {cpd_distance} {%+6s %.6s} {$checkpoints[$vcpv][distance]};
                    #format {cpd_note} {%-20s %.20s} {$cp_roomnote};
                    #format {cpd_short} {%-20s %.20s} {$cp_roomdesc};
                    #format {cpd_roomid} {%+6s %.6s} {$vcpv};
                    #format {cp_command} {%+6s} {run${cpcmd}};
                    #var cplist[$cpcmd] {$cpd_distance $c_rooms[$zadania[$zadd]]$cpd_area:<088> $cp_roomnote};
                    #nop sh {$cpd_runcmd Dyst: $cpd_distance $c_rooms[$zadania[$zadd]]$cpd_area:<088> $cp_roomnote} {$cpcmd} {1};
                    #math {cpcmd} {$cpcmd + 1};
                };
            };
        };
    };

    #sh {<ABA><178> MAPA <088>} {1} {1};
} {1};

#action {<PORT> New connection: %* D%d.} {#var connectorId %2;};
#action {<PORT> Closing connection to %*} {
    #port uninit;
    #end {Polaczenie zostalo przerwane. Uzyj komendy<128>./ttmux<088> aby uruchomic ponowie};
}
#action {<PORT> GOTO %d} {
    #map goto %1;
}
