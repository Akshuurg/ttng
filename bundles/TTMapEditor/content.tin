#nop #################################################################################;
#nop Edycja map;
#nop #################################################################################;
#alias {TTMapEditorAddress} {
    #var tmp {$ConfigManager_Configs[TTMapEditorAddress]};
    #replace tmp {,} {;};
    #list connstr create {$tmp};
    #unvar tmp;
    #if {"$PONGED" == "1"} {
        #chat zap brain;
    } {
        #if {"$connstr[1]" != ""} {
            #if {"$connstr[2]" != ""} {
                #delay {3} {#chat call {$connstr[1]} {$connstr[2]}};
            };
        };
    };
};
#action {<CHAT> %*ZNALEZIONY} {
    #line gag;
} {8};

#action {Connection made to brain.} {
    #var PONGED 1;
    #nop action {<CHAT>} {#line gag} {8};
};

#action {brain chats to everyone, 'MCREATE %1 %2'} {
    #map at {%1} {#map dig %2};
    #chat message mapka {MCREATE %1 %2};
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Tworze pokoj.<088>};
    };
    #line gag;
} {1};

#action {brain chats to everyone, 'MARK %d %w'} {
    #map at {%1} {#map set roomarea %2};
    #map at {%1} {#map set roomcolor $c_rooms[%2]};
    #map at {%1} {#map set roomarea %2};
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Ustawiam kolor.<088>};
    };
    #chat message mapka {MARK %1 %2};
    #line gag;
} {1};

#action {brain chats to everyone, 'MARK %d %w %*'} {
    #map at {%1} {#map set roomarea %2};
    #map at {%1} {#map set roomcolor $c_rooms[%2]};
    #map at {%1} {#map set roomnote %3};
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Ustawiam kolor.<088>};
    };
    #chat message mapka {MARK %1 %2};    
	#line gag;
} {1};

#action {brain chats to everyone, 'DEL %1 %2'} {
    #map at {%1} {#map del %2};
    #chat message mapka {DEL %1 %2};
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Usuwam pokoj.<088>};  
    };
    #line gag;
} {1};

#action {brain chats to everyone, 'NOTE %1 %*'} {
    #map at {%1} {#map set roomsymbol {<178>i<088>}};
    #chat message mapka {NOTE %1};
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Dodaje notke.<088>};
    };
    #line gag;
} {1};

#action {brain chats to everyone, 'EVENT %d %*'} {
    #map at {%1} {#map set roomsymbol {<168>@<088>}};
    #chat message mapka {EVENT %1};
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Dodaje event.<088>};
    };
    #event {MAP ENTER ROOM $tmproomvnum} {QB %2};
    #line gag;
} {1};

#action {brain chats to everyone, 'COMMAND %d %*'} {
    #map at {%1} {#map set roomsymbol {<138>E<088>}};
    #chat message mapka {COMMAND %1};
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Dodaje auto-komende.<088>};
    };
    #event {MAP ENTER ROOM $tmproomvnum} {%2};
    #line gag;
} {1};


#action {brain chats to everyone, 'ROOMSHORT %d %*'} {
    #map at {%1} {
        #map get roomsymbol vroomsymbol;
        #map set roomdesc {%2};
        #chat message mapka {ROOMSHORT %1};
        #if {"$vroomsymbol" == "<faa>?<088>"} {
            #map set roomsymbol {};
        };

    };
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Dodaje krotki opis.<088>};
    };
    #line gag;
};

#action {brain chats to everyone, 'ROOMEXITS %d %*'} {
    #map at {%1} {
        #map set roomname {%2};
        #map get roomcolor vroomcolor;
        #if {"$vroomcolor" == "<faa>"} {
            #map set roomcolor {};
        };
    };
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Dodaje dostepne wyjscia.<088>};
    };    
	#nop line gag;
};

#action {brain chats to everyone, 'NAME %d %*'} {
    #map at {%1} {
        #map set roomnote {%2};
    };
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Dodaje typ pokoju.<088>};
    };    
	#line gag;
}

#action {brain chats to everyone, 'HIDE %d %w'} {
    #map at {%1} {
        #line substitute variables {#map exitflag %2 hide on};
    };
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Ukrywam pokoj.<088>};
    };
    #chat message mapka {HIDE %1 %2};
    #line gag;
}

#action {brain chats to everyone, 'UNHIDE %d %w'} {
    #map at {%1} {
        #line substitute variables {#map exitflag %2 hide off};
    };
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Odslaniam pokoj.<088>};
    };
    #chat message mapka {UNHIDE %1 %2};
    #line gag;
};

#action {brain chats to everyone, 'LINKB %d %w %d'} {
    #map at {%1} {
        #line substitute variables {
            #map link %2 %3 both;
        };
    };
    #chat message mapka {LINKB %1 %2 %3};
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Lacze pokoje dwostronnie.<088>};
    };    
	#line gag;
};

#action {brain chats to everyone, 'UNLINKB %d %w'} {
    #map at {%1} {
        #line substitute variables {
            #map unlink %2 both;            
        };
    };
    #chat message mapka {UNLINKB %1 %2};
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Rozlaczam pokoje dwostronnie.<088>};
    };    
	#line gag;
};

#action {brain chats to everyone, 'LINK %d %w %d'} {
    #map at {%1} {
        #line substitute variables {
            #map link %2 %3;
        };
    };
     #chat message mapka {LINK %1 %2 %3};
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Lacze pokoje jednostronnie.<088>};
    };   
	#line gag;
};

#action {brain chats to everyone, 'UNLINK %d %w'} {
    #map at {%1} {
        #line substitute variables {
            #map unlink %2;         
        };
    };
    #chat message mapka {UNLINK %1 %2};
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Rozlaczam pokoje jednostronnie.<088>};
    };    
	#line gag;
};

#action {brain chats to everyone, 'MEXIT %d %w %d %*'} {
    #map at {%1} {
        #map exit %2 command {%4};
        #map link {%4} %3;
    };
    #if {$profiles_mapdebug == 1} {
        #echo {${c_arkatt}> Mapa: Dodaje wyjscie specjalne.<088>};
    };    
	#line gag;
};

#action {brain chats to everyone, 'COLOR %d %S'} {
    #var tmpsymbol {%2};
    #replace {tmpsymbol} {-} {<};
    #replace {tmpsymbol} {+} {>};
    #map at {%1} {#map set roomcolor {$tmpsymbol}};
    #chat message mapka {COLOR %1 %2};
    #line gag;
};

#alias {show_ponged_error} {
    #echo {${c_stan_niski}> Mapa: niepolaczona!};
};

#alias {/r$} {
    #if {$PONGED == 1} {
        #map get roomvnum tmproomvnum;
        #chat message brain {COLOR $tmproomvnum R}    
    } {show_ponged_error};
} {1};
#alias {/y$} {
    #if {$PONGED == 1} {
    #map get roomvnum tmproomvnum;
    #chat message brain {COLOR $tmproomvnum Y}
    } {show_ponged_error};
} {1};

#alias {/c$} {
    #if {$PONGED == 1} {
    #chat message brain {CLEAR};
    } {show_ponged_error};
} {1};

#alias {/disc$} {
    #if {$PONGED == 1} {
    #map get roomvnum voom;
    #chat message brain {ROOMEXITS $voom $current_room_exits};
    #chat message brain {ROOMSHORT $voom $current_room_short_desc};
    } {show_ponged_error};
} {1};

#alias {/map link %w %d} {
    #if {$PONGED == 1} {
    #map get roomvnum voom;
    #chat message brain {LINK $voom %1 %2};
    } {show_ponged_error};
} {1};

#alias {/map linkb %w %d} {
    #if {$PONGED == 1} {
    #map get roomvnum voom;
    #chat message brain {LINKB $voom %1 %2};
    } {show_ponged_error};
} {1};

#alias {/map unlinkb %w} {
    #if {$PONGED == 1} {
    #map get roomvnum voom;
    #chat message brain {UNLINKB $voom %1};
    } {show_ponged_error};
} {1};

#alias {/map unlink %w} {
    #if {$PONGED == 1} {
    #map get roomvnum voom;
    #chat message brain {UNLINK $voom %1};
    } {show_ponged_error};
} {1};

#alias {/map hide %w} {
    #if {$PONGED == 1} {
    #map get roomvnum voom;
    #chat message brain {HIDE $voom %1};
    } {show_ponged_error};
} {1};

#alias {/map unhide %w} {
    #if {$PONGED == 1} {
    #map get roomvnum voom;
    #chat message brain {UNHIDE $voom %1};
    } {show_ponged_error};
} {1};

#alias {/map dig %w} {
    #if {$PONGED == 1} {
    #map get roomvnum v;
    #chat message brain {MCREATE $v %1};
    } {show_ponged_error};
} {1};

#alias {/mark as %w$} {
    #if {$PONGED == 1} {

    #switch {"%1"} {
        #case {"vendor"} {};
        #case {"checkpoint"} {};
        #case {"water"} {};
        #case {"bank"} {};
        #case {"post"} {};
        #case {"safe"} {};
        #case {"food"} {};
        #case {"quest"} {};
        #case {"info"} {};
        #case {"danger"} {};
 	#case {"port"} {};
	#case {"herb"} {};
	#case {"trap"} {};
	#case {"smith"} {};
 	#case {"training"} {};
	#case {"knowledge"} {};
	#case {"storage"} {};
        #default {
            #echo {Niedozwolona wartosc: %1};
            #return;
        };
    }
    #map get roomvnum v;
    #chat message brain {MARK $v %1 $temp_short};
    } {show_ponged_error};
} {1};

#alias {/mark as %w %*} {
    #if {$PONGED == 1} {

    #switch {"%1"} {
        #case {"vendor"} {};
        #case {"checkpoint"} {};
        #case {"water"} {};
        #case {"bank"} {};
        #case {"post"} {};
        #case {"safe"} {};
        #case {"food"} {};
        #case {"quest"} {};
        #case {"info"} {};
        #case {"danger"} {};
 	#case {"port"} {};
	#case {"herb"} {};
	#case {"trap"} {};
	#case {"smith"} {};
 	#case {"training"} {};
	#case {"knowledge"} {};
	#case {"storage"} {};
        #default {
            #echo {Niedozwolona wartosc: %1};
            #return;
        };
    }
    #map get roomvnum v;
    #chat message brain {MARK $v %1 %2};
    } {show_ponged_error};
} {1};

#alias {/name} {
    #if {$PONGED == 1} {

    #map get roomvnum v;
    #chat message brain {NAME $v %0};
    } {show_ponged_error};
} {1};

#alias {/map del %w} {
    #if {$PONGED == 1} {
    #map get roomvnum v;
    #chat message brain {DEL $v %1};
    } {show_ponged_error};
} {1};

#alias {/map wyj %w %*} {
    #if {$PONGED == 1} {
    #map get roomvnum v;
    #chat message brain {MEXIT $v %1 %2};
    #nop chat message mapka {MEXIT $v %1 %2};
    } {show_ponged_error};
} {1};

#alias {/note %*} {
    #if {$PONGED == 1} {
        #map get roomvnum voom;
        #chat message brain {NOTE $voom %1};
    } {show_ponged_error};
} {1};

#alias {/note$} {
    #map get roomvnum voom;
} {1};

#action {<CHAT> File transfer from brain, file: %d, size: %d.} {
    #chat message mapka {DOWNLOAD notke %1};
    #chat accept brain;
} {1};

#action {<CHAT> Transfer of %d completed} {
    #line substitute {variables} {
        #system {mv %1 $path/notes/%1};
    };
} {1};

#alias {/event %*} {
    #if {$PONGED == 1} {
        #map get roomvnum tmproomvnum;
        #chat message brain {EVENT $tmproomvnum %1};
    } {show_ponged_error};
} {1};

#alias {/command %*} {
    #if {$PONGED == 1} {
        #map get roomvnum tmproomvnum;
        #chat message brain {COMMAND $tmproomvnum %1};
    } {show_ponged_error};
} {1};

#var c_rooms[vendor] {<158>};
#var c_rooms[checkpoint] {<168>};
#var c_rooms[water] {<148>};
#var c_rooms[bank] {<138>};
#var c_rooms[post] {<128>};
#var c_rooms[safe] {<afa>};
#var c_rooms[food] {<128>}
#var c_rooms[quest] {<138>};
#var c_rooms[info] {<178>};
#var c_rooms[danger] {<118>};
#var c_rooms[port] {<118><ccf>};
#var c_rooms[herb] {<118><fac>};
#var c_rooms[trap] {<FAA><178>};
#var c_rooms[smith] {<118><caf>};